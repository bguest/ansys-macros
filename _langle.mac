!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!LAngle.mac
!B.Guest - benguest@gmail.com
!DESCRIPTION: This macro calculates the angle between
!	two straight lines using the dot product of vectors
!PRECONDITIONS
!	ARG1 = first line, else user is prompted to select
!		lines
!	ARG2 = second line, else user is prompted to slect
!		lines
!POSTCONDITIONS:
!	theta = angle between lines
!	Coordinate systems set to 0.
!======================================================
CSYS,0
CM,temp_LINE,LINE

! Get L*sines -------------------------------------------

*IF,ARG1,EQ,0,THEN !-------------------Select Lines
   *MSG,UI
   SELECT Two (2) LINES
   
   LSEL,R,P
   line1= $ *GET,line1,LINE,,NUM,MAX !GET SELECTED LINE NUMBER
*ELSE
   line1=ARG1
*ENDIF

*IF,ARG2,EQ,0,THEN !-----------------SECOND LINE

   line2= $ *GET,line2,LINE,,NUM,MIN !GET SELECTED LINE NUMBER
*ELSE
   line2=ARG2
*ENDIF
CMSEL,S,TEMP_LINE

! Calculations ========================================

KP1= $ *GET,KP1,LINE,Line1,KP,1   !GET KP AT ONE END
P1= $ *DIM,P1,ARRAY,3
P1(1) = KX(KP1)				!Get location of kp1
P1(2) = KY(KP1)
P1(3) = KZ(KP1)

KP2= $ *GET,KP2,LINE,Line1,KP,2   !GET KP AT OTHER END
P2= $ *DIM,P2,ARRAY,3
*GET,P2(1),KP,KP2,LOC,X    !GET LOCATION OF KP2
*GET,P2(2),KP,KP2,LOC,Y
*GET,P2(3),KP,KP2,LOC,Z

KP3= $ *GET,KP3,LINE,Line2,KP,1   !GET KP AT ONE END
P3= $ *DIM,P3,ARRAY,3
!*GET,Par,KP,N,Item1,IT1NUM,
*GET,P3(1),KP,KP3,LOC,X    !GET LOCATION OF KP3
*GET,P3(2),KP,KP3,LOC,Y
*GET,P3(3),KP,KP3,LOC,Z

KP4= $ *GET,KP4,LINE,Line2,KP,2   !GET KP AT OTHER END
P4= $ *DIM,P4,ARRAY,3
*GET,P4(1),KP,KP4,LOC,X    !GET LOCATION OF KP4
*GET,P4(2),KP,KP4,LOC,Y
*GET,P4(3),KP,KP4,LOC,Z

!Calculate vectors
av = $ *dim,av,array,3
bv = $ *dim,bv,array,3
*do,i,1,3
	av(i) = P2(i)-P1(i)
	bv(i) = P4(i)-P3(i)
*enddo

!Calculate Dot product and vector lengths
ab = 0 !a.b
aa = 0 !||a||
bb = 0 !||b||
*do,i,1,3
	ab = ab+av(i)*bv(i)
	aa = aa+av(i)**2
	bb = bb+bv(i)**2
*enddo

theta = acos(ab/sqrt(aa*bb))

*msg,WARN,theta
Angle = %f

!======THE--END========THE--END========THE--END========
!ITS THE END OF THE FILE AS WE KNOW IT, AND I FEEL FINE

