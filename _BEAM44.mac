!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!_BEAM44.mac
!B.Guest - BenGuest@gmail.com
!DESCRIPTION: This macro quickly defines a beam44 
!	element type and material property
!PRECONDITIONS:
!  B44ShearY: Shear constant y direction (optional)
!  B44ShearZ:   "      "     Z    "          "
!	ARGUMENTS:
!		ARG1: Resulting element property number
!		ARG2: Area
!		ARG3: IXX (X = Axis) (Optional)
!		ARG4: IYY
!		ARG5: IZZ
!		ARG6: Material (See _MAT.mac)
!======================================================
MatNum = ARG1

*if,ARG2,EQ,0,then
   AA = 0
*else
   AA = ARG2
*endif

*if,ARG4,EQ,0,then
   IYY = 1/12
*else
   IYY = ARG4
*endif

*if,ARG5,EQ,0,then
   IZZ = 1/12
*else
   IZZ = ARG5
*endif

*if,ARG3,EQ,0,then
   IXX = IYY+IZZ
*else
   IXX = ARG3
*endif

*if,B44ShearZ,eq,0,then
   B44ShearZ = 0
*endif

*if,B44ShearY,eq,0,then
   B44ShearY = 0
*endif

!TKY, TKZ are calculated assuming as square x-section
TKZ = (144*Iyy**3/Izz)**(1/8)
TKY = (144*Izz**3/Iyy)**(1/8)

!------------------------------------------------------
ET,MatNum,BEAM44
R,MatNum, AA, IZZ, IYY, TKZ/2, TKY/2, IXX, 
RMORE,    AA, IZZ, IYY, TKZ/2, TKY/2, IXX, 
RMORE, , , , , , , 
RMORE,B44ShearZ,B44ShearY, TKZ/2, TKY/2, TKZ/2, TKY/2, 

!Set material
*if,arg6,gt,0,then
	_MAT,arg6,MatNum
*endif

!Cleanup
B44ShearZ = $ B44ShearY =

!======THE--END========THE--END========THE--END========
!ITS THE END OF THE FILE AS WE KNOW IT, AND I FEEL FINE