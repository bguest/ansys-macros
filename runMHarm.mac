!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!runMHarm.mac
!benguest@gmail.com
!DESCRIPTION: This macro preforms a base excitation
! modal superposition harmoic analysis on a model with
! 'basenodes' using the large mass method.
!PRECONDITIONS:
!  Components of of nodes to be constrained for base
!    movement specified as "BaseNodes"
!  ARG1 = Imput Direction (1=X,2=Y,3=Z)
!    ELSE user is prompted to enter a direction
!  ARG2 = Number of Modes ELSE:
     NModes=200
!  ARG3 = Start Frequence: ELSE:
     FMin=0
!  ARG4 = End Frequency ELSE:
     FMax=200
!  ARG5 = Number of Frequency Points per Mode, ELSE:
     NumPts=6
!  ARG6 = Structural Damping Ration (DMPRAT)
    DampRatio=0.015
!POSTCONDITIONS:
!  See Description. File,,rfrq /GROPT,LOGX,ON  /GROPT,LOGY,ON
!NOTES: NOT COMPLEATE, DEF NEEDS SOME WORK!
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CM,temp_nodes,Node
CM,temp_kps,kp
!-----------------------------------------GED DIRECTION
*IF,ARG1,EQ,0,THEN
   *ASK,ANS,ENTER DIRECTION (1=X,2=Y,3=Z),3
*ELSE
   ANS = ARG1
*ENDIF

*IF,ANS,EQ,1,THEN
   direction='UX'
   fdirect = 'FX'
*ELSEIF,ANS,EQ,2,THEN
   direction='UY'
   fdirect = 'FY'
*ELSEIF,ANS,EQ,3,THEN
   direction='UZ'
   fdirect = 'FZ'
*ENDIF
!--------------------------------SETUP
*IF,ARG2,NE,0,THEN !Set Number of Modes
   NModes=ARG2
*ENDIF

*IF,ARG3,NE,0,THEN !Set Minimum Frequency
   FMin=ARG3
*ENDIF

*IF,ARG4,NE,0,THEN !Set Maximum Frequency
   FMax=ARG4
*ENDIF

*IF,ARG5,NE,0,THEN !NumPts=(defined above)
   NumPts=ARG5
*ENDIF

*IF,ARG6,NE,0,THEN !DampRatio Defined Above
  DampRatio=ARG6
*ENDIF
!======================================================
!LARGE MASS
!------------------------------------------------------
/prep7
MASS = 10E3 	 !LARGE MASS Properties
ET,701,MASS21
R,701,MASS,MASS,MASS	 

!Initial boundary conditions
CMSEL,S,BaseNodes
ddele,all,direction
dkdele,all,direction
ksln
katt,701,701,701
kmesh,all
F,ALL,fdirect,1.0*MASS
cmsel,s,temp_kps
cmsel,s,temp_nodes

!=================MODAL ANALYSIS=======================
/solu
/nerr,,2**30
ANTYPE,MODAL                      ! Modal analysis
MODOPT,LANB,NModes,FMin,FMax      ! Block Lancozos
MXPAND,NModes,FMin,FMax,YES       !EXPAND MODES
/OUTPUT,OUT_Modes,out,,
SOLVE                       ! Initiate modal solution
FINISH
/OUTPUT

!=========Superposition Harmonic Solution==============
!  Obtain the Mode Superposition Harmonic Solution
/SOLU             ! Enter SOLUTION
ANTYPE,HARMIC     ! Harmonic analysis
HROPT,MSUP,       ! Mode superposition method;
HROUT,OFF,ON       ! Harmonic analysis output options; cluster option
!LVSCALE,          ! Scale factor for loads from modal analysis
HARFRQ,FMin,FMax    ! Forcing frequency range
NSUBST,NumPts       ! Number of harmonic solutions
DMPRAT,DampRatio    ! Damping Ratio
!MDAMP,...          ! Modal damping ratios
KBC,1               ! Ramped or stepped loads
!OUTRES,,1
OUTPR,NSOL
/OUTPUT,OUT_MHarm,out,
SOLVE                      ! Initiate solution
FINISH
/OUTPUT
CMSEL,s,baseNodes
!======================================================
! Cleanup
/prep7
D,ALL,direction,0
fdele,all,fdirect
ksln
kclear,all
cmsel,s,temp_kps
cmsel,s,temp_nodes
!======THE--END========THE--END========THE--END=========
!ITS THE END OF THE MACRO AS WE KNOW IT, AND I FEEL FINE

! !  Review the Results of the Mode Superposition Solution
! /POST26
! FILE,,RFRQ                 ! Postprocessing file is Jobname.RFRQ
! NSOL,...                   ! Store nodal result as a variable
! PLCPLX,...                 ! Define how to plot complex variables
! PLVAR,...                  ! Plot variables
! FINISH

! !  Expand the Solution (for Stress Results)
! /SOLU! Re-enter SOLUTION
! EXPASS,ON                  ! Expansion pass
! EXPSOL,...                 ! Expand a single solution
! HREXP,...                  ! Phase angle for expanded solution
! SOLVE
! FINISH

! ! Review the Results of the Expanded Solution
! /POST1
! SET,...                   ! Read results for desired frequency
! PLDISP,...                ! Deformed shape
! PLNSOL,...                ! Contour plot of nodal results
! ---
! FINISH