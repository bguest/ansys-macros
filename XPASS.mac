/PMACRO
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!XPASS.mac
!B.GUEST: bagvoo@gmail.com
!DESCRIPTION: This macro expands a superelement 
!PRECONDITIONS:
!   ARG1=Super element to be expanded
!	 ARG2=Super Element Use Name
!   ARG3=Load Step
!   ARG4=SubStep
!   Superelement used in file.db !!!!
!POSTCONDITIONS:
!   See Description
!   LSTEP = Load Slized
!   SBSTEP = Substep Analized
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
SAVE,saved,db,,ALL
!----------------------------------------GET PARAMETERS
DBname=ARG1
SEname=ARG2
*IF,ARG3,EQ,0,THEN !------------GET LOADSTEP
   *GET,LSTEP,ACTIVE,,SET,LSTP
*ELSE
   LSTEP=ARG3
*ENDIF
*IF,ARG4,EQ,0,THEN !------------GET SUBSTEP
   *GET,SBSTEP,ACTIVE,,SET,SBST
*ELSE
   SBSTEP=ARG4
*ENDIF
PARSAV,ALL,XPASS,tmp   ! SAVE PARAMETERS
!-------------------------RESUME SUPERELEMENT DATABASE
FINISH
/CLEAR               ! Clear the database
PARRES,,XPASS,tmp    ! RESUME PARAMETERS
/FILNAME,DBname      ! Change jobname
RESUME               ! Restore generation pass database
PARRES,,XPASS,tmp    ! RESUME PARAMETERS
/SOLU                ! Enter SOLUTION
EXPASS,ON            ! Activate expansion pass
SEEXP,SEname,file    ! Superelement name to be expanded (GEN, unless SETRAN used)

EXPSOL,LSTEP,SBSTEP,,YES ! Load step options (mainly output controls)
OUTRES,ALL,ALL
SOLVE                ! Initiate expansion pass solution.  Full
                     !   superelement solution written to GEN.RST (or
                     !   RTH or RMG).
/DSCALE,1,1
!======THE--END========THE--END========THE--END========
!ITS THE END OF THE MACRO AS WE KNOW IT AND I FEEL FINE