!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
!_MAT.mac
!DESCRIPTION: Creates a standard material from a single
!  number
!PRECONDITIONS:
!  - UNITS have been set prior to running tis command as
!     there is no way to tell what units your model
!     uses
!	mFactor (optional) mass factor ie 1.10 = 10% extra mass
!  flagPrintMatProps (optional)
!     1 -> Material Properties are output to OUT_MaterialProperties.out (Coma delimiated)
!     0 -> No material properties are output (Default)
!  ARG1 = Material Number
!		802 -> Aluminum 7075-T73
!		803 -> Titanium Ti-6Al-4V
!		804 -> Stainless Steel A286
!		805 -> Aluminum 6061-T651
!		806 -> Stainless Steel 15-5 PH
!		807 -> G10 (XY/Z) Orthotropic
!     899 -> Call to Material File Footer
!	ARG2 = Resulting material number
!		[Default] = ARG1
!POSTCONDITIONS:
!  See Description.
!&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
/PREP7
!Setup Header and output file if flagPrintMatProps Set
*if,flagPrintMatProps,eq,1, and, flagMatHeader,lt,1,then
   flagMatHeader = 1
   *CFOPEN,OUT_MaterialProperties,out
   *vwrite
Material,Material,Modulus (EX),Poisons,CTE (X),Density,Mass,Reference
   *vwrite
ID,Name,psi,Ratio (nuxy),in/in/degC,lbm/in^3,Factor,
*endif

!Run Material Footer, Close Down File
*if,arg1,eq,899, and, flagPrintMatProps, eq,1, then
   flagMatHeader = 0
   flagPrintMatProps = 0
   *CFCLOS
   *GO,:_matEnd !RETURN
*endif

!Mass Factors
*if,mFactor,le,0,then	!Account for mass factors
   mFactor = 1
*endif

*if,arg2,gt,0,then	!Set material number
	MNum = ARG2
*else
   MNum = ARG1
*endif

! Units
*if,psi,eq,0,then
   UNITS
*endif

!---------- STANDARD MATERIAL PROPERTIES -------------

!DEFAULTS
MatName  = 'No Material Assigned'
ref      = 'Default Material'
E        = 1
CTEX     = 0
nu_XY    = 0.3
rho      = 1

*IF,ARG1,EQ,802,THEN
	MatName = 'Aluminum 7075-T73'
   ref     = 'MIL-HDBK-5E'
	E       = 10.3e6*psi
	rho     = 0.1*lbm/in**3
   cteX    = 12.4E-6/degF
   nu_XY   = 0.33
	! !----FAILURE CRITERIA (YEILD STRENGTHS)
	! FC,MNum,S,XTEN,56e3 !Tensile stress, X-direction
	! FC,MNum,S,YTEN,56e3 !Tensile stress, y-direction
	! FC,MNum,S,ZTEN,56e3 !Tensile stress, Z-direction 
	! FC,MNum,S,XY  ,38e3 !XY shear stress(Ultimate Strength)
	! FC,MNum,S,YZ  ,38e3 !YZ shear stress(Ultimate Strength)
	! FC,MNum,S,XZ  ,38e3 !XZ shear stress(Ultimate Strength)

*ELSEIF,ARG1,EQ,803,THEN
   MatName = 'Ti-6Al-4V'
   ref     = 'MIL-HDBK-5E'
   E       = 16.0e6*psi
   rho     = 0.1*lbm/in**3
   nu_XY   = 0.34
   cteX    = 4.8e-6/degF !AT 70degF

*ELSEIF,ARG1,EQ,804,THEN
   MatName = 'STAINLESS_STEEL_A286'
   ref     = 'PRO_E'
   E       = 2.91E+07*psi
   rho     = 2.87E-01*lbm/in**3
   cteX    = 9.20E-06/degF
   nu_XY   = 3.10E-01

*elseif,ARG1,EQ,805,THEN
   MatName = 'Aluminum 6061-T651'
   ref     = 'MIL-HDBK-5E'
   E       = 10.0e6
   rho     = 0.098*lbm/in**3
   nu_XY   = 0.33
   cteX    = 12.65E-6/degF  !AT 70degF

*elseif,ARG1,EQ,806,THEN
   MatName = 'STAINLESS_STEEL_15-5PH (H1025)'
   ref     = 'MIL-HDBK-5E and PRO_E'
   E       = 28.5e6*psi
   rho     = 0.283*lbm/in**3
   nu_XY   = 0.27
   cteX    = 6.3e-6/degF  !AT 70degF

*elseif,ARG1,eq,807,then
	! G10 Orthotropic material (See 110602_G10_Material_Review)
   MatName = 'G10 Orthotropic'
   ref     = 'See 110602_G10_Material_Review'
   E       = 2.7e6*psi
   rho     = 0.065*lbm/in**3
	MP,EY,MNum,2.7e6*psi
	MP,EZ,MNum,2.3e6*psi
*ENDIF

!Sub Set Values Above
MP,EX,MNum,E,
MP,DENS,MNum,rho*mFactor
MP,PRXY,MNum,nu_XY
MP,ALPX,MNum,cteX

*if,flagPrintMatProps,EQ,1,then
	*vwrite, MNum, MatName, E/psi, nu_XY, cteX*degC, rho*in**3/lbm, mFactor, ref
%I,%C,%G,%G,%G,%G,%G,%C
*endif

:_matEnd
!======THE--END========THE--END========THE--END========
!ITS THE END OF THE FILE AS WE KNOW IT, AND I FEEL FINE